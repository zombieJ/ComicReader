<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009"
					   xmlns:s="library://ns.adobe.com/flex/spark"
					   xmlns:mx="library://ns.adobe.com/flex/mx"
					   width="650" height="350" applicationComplete="setup(event)">
	
	<fx:Script>
		<![CDATA[
			import com.coltware.airxzip.ZipEntry;
			import com.coltware.airxzip.ZipFileReader;
			
			import comic.Page;
			
			import mx.collections.ArrayList;
			import mx.controls.Alert;
			import mx.events.FlexEvent;
			import mx.graphics.codec.JPEGEncoder;
			
			import spark.events.IndexChangeEvent;

			protected var reader:ZipFileReader;
			protected var loader:Loader = new Loader();

			//================================================
			public function get imgWidth():int {
				return loader.contentLoaderInfo.width;
			}
			public function get imgHeight():int {
				return loader.contentLoaderInfo.height;
			}
			//================================================

			protected function setup(event:FlexEvent):void {
				// Get image size
				loader.contentLoaderInfo.addEventListener(Event.INIT, function(e:Event):void {
					var b:BitmapData = new BitmapData(imgWidth, imgHeight);
					b.draw(loader);
					loadImage(b);
				});

				// Init
				reader = new ZipFileReader();
				var file:File = new File("E:\\TDDOWNLOAD\\Don Rosa\\01-The Son of the Sun.cbz");
				reader.open(file);
				var entry:ZipEntry;
				var list:ArrayList = filterEntries(reader.getEntries());
				lst_images.dataProvider = list;
				
				var page:Page = Page(list.getItemAt(0));
				loadPage(page);
			}

			public function filterEntries(list:Array):ArrayList {
				var ret:ArrayList = new ArrayList();
				for each(var entry:ZipEntry in list) {
					if(entry.getFilename().indexOf("__") != 0 && !entry.isDirectory()) {
						ret.addItem(new Page(entry));
					}
				}
				return ret;
			}
			
			protected function lst_images_changeHandler(event:IndexChangeEvent):void {
				var page:Page = lst_images.selectedItem;
				loadPage(page);
			}

			protected function loadPage(page:Page):void {
				var data:ByteArray = reader.unzip(page.entry);
				loader.loadBytes(data);
			}

			protected function loadImage(data:BitmapData):void {
				updateScale();
				img_loader.source = data;
			}
			
			protected function updateScale(event:Event = null):void {
				var size:Number = sld_scale.value;

				img_loader.width = imgWidth * size / 100;
				img_loader.height = imgHeight * size / 100;
				this.title = img_loader.width + "/" + img_loader.height;
			}

			// User move image position
			private var prePos:Point;
			private var preMosPos:Point;
			protected function grp_img_mouseDown(e:MouseEvent):void {
				prePos = new Point(img_loader.x, img_loader.y);
				preMosPos = new Point(e.stageX, e.stageY);
			}

			protected function grp_img_mouseMove(e:MouseEvent):void {
				if(preMosPos == null) return;
				img_loader.x = prePos.x + e.stageX - preMosPos.x;
				img_loader.y = prePos.y + e.stageY - preMosPos.y;
			}

			protected function grp_img_mouseUp(e:MouseEvent):void {
				preMosPos = null;
			}
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<s:Group id="grp_img" left="0" right="218" top="0" bottom="0"
			 mouseDown="grp_img_mouseDown(event)"
			 mouseMove="grp_img_mouseMove(event)"
			 mouseUp="grp_img_mouseUp(event)"
			 clipAndEnableScrolling="true">
		<s:Image id="img_loader" scaleMode="stretch" smooth="true" smoothingQuality="high"/>
	</s:Group>
	<s:Group right="10" top="10" bottom="10" width="200">
		<s:List id="lst_images" top="19" bottom="0" width="100%"
				change="lst_images_changeHandler(event)" labelField="filename"></s:List>
		<s:HSlider id="sld_scale" width="100%" change="updateScale(event)" maximum="300"
				   minimum="10" stepSize="10" value="100"/>
	</s:Group>
</s:WindowedApplication>
